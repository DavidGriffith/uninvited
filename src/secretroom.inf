! UNINVITED
! Copyright (c) 1986 by Icom.
! Reimplemented in Inform by David Griffith (c) 2012
!

Mansion SecretRoom "Secret Room"
	with name "secret" "room",
	description [;
		print "This small, claustrophobic chamber is stuffy and 
		cold.  It has a single large dirty window.  The air in 
		here is so thick that you can barely catch your breath. ";
		if (IronHatch has open)
			"An iron hatch hinged to cover the hole in the 
			floor from whence you emerged leans against the 
			wall.";
		"An iron hatch covers the wet hole in the floor from 
		whence you emerged.";
	],
	d_to [;
		deadflag = 1;
		"As jump down into the dark, water-filled bathroom, you 
		bump your head on the rim of the hole.  In your 
		disorientation, you lose track of the hole.  You thrash 
		about, frantically searching for the hole.  You fail to 
		find the hole.";
	],
	out_to [;
		if (SecretWindow hasnt general)
			"The glass of the window blocks your exit.";
		if (BrotherDemon in self) {
			deadflag = 1;
			"The fiendish spectre prevents you from 
			following your brother out the window.  He has 
			no intention of letting you leave, ever.  
			Without quarter, he overwhelms your being and 
			floods your mind with the tide of his evil 
			will.  Neither living nor dead, you must now 
			endure the hideous realm of the undead.";
		}
		return OutsideWin;
	];


Object -> IronHatch "iron hatch"
	with name "hatch" "iron",
	description [;
		print "This heavy, iron hatch is hinged to cover 
			the wet hole in the floor.";
		if (self has open)
			" Through the hole, you see a bathroom 
			completely filled with water.";
		"";
	],
	before [;
	Search:
		<<examine self>>;
	],
	door_to Bathroom,
	door_dir d_to,
	has scenery door openable open;


Object -> SecretWindow "window"
	with name "window" "glass",
	description [;
		if (self has general)
			"Cool air gently blows through the broken 
			window.  You can see the burned wreckage of your 
			car from here.";
		"Because of the grime on the window, you're not sure 
		which side of the house you're looking out from.";
	],
	before [;
	Search:
		<<examine self>>;
	Attack:
		if (self hasnt general) {
			give self general;
			"You shatter the window.";
		}
		"But it's already broken.";
	Enter:
		<<Go out_obj>>;
	],
	has scenery;	

Object -> Brother "brother"
	with name "brother" "boy",
	article "your",
	move_type NO_MOVE,
	caprice 0,
	description "There is a very strange look in your brother's 
		eyes.  They are not his own.  A low baritone voice 
		oozes from his mouth.  He curses and threatens you, but 
		there is pain and fear in his face.",
	describe "^Your brother babbles curses and blasphemies.  Every 
		few seconds he emits a loud shriek.",
	life [;
	Give, Show:
		if (noun == Cross) {
			"As you thrust the cross forward toward your 
			brother you realize this isn't a movie.  You 
			wonder what you are trying to accomplish by 
			this action.";
		}
		"Your brother continues to babble.";
	],
	before [;
	Smell:
		"He smells of fear.";
	Attack:
		if (second == nothing) {
			print "You attempt to smack some sense into your 
			brother...^^";
			remove self;
			move BrotherDemon to Location;
			BrotherDemon.emerge();
			rtrue;
		}
		"That's probably not a good idea.  Your brother is 
		already hurting.";
	],
	has animate;

Object BrotherDemon "demon"
	with name "smelly" "demon" "daemon",
	description "It's a demonic spectre.  The glowing yellow eyes, 
		the horns, sulphurous smell, and dark red complexion are 
		horrible.  Truly this thing is the spawn of hell.",
	movecount 0,
	daemon [;
		switch(self.movecount++) {
		0:	"^", (The) self, " snarls and roars at you.";
		1:	"^", (The) self, " shouts assorted blasphemies.";
		2:	"^", (The) self, " laughs menacingly.";
		3:	"^", (The) self, " reaches towards you.";
		default:
			print "^";
			StopDaemon(self);
			self.killplayer();
		}
	],
	emerge [;
		StartDaemon(self);
		print "Your brother's expression grows blank and a misty 
		spectre grows out of his body.  A demonic visitor
		appears and begins wailing these foreboding words.  
		~This little one is too weak for me.  I need new blood 
		and a stronger body.  I need you.  I will have you.  You 
		will join us...now!~  The hideous creature now hovers, 
		threateningly, in front of you.^
		^
		Your brother, now freed from the evil demon that was 
		dwelling within, ";
		if (SecretWindow has general)
			print "runs out through the shattered window";
		else
			print "smashes the window and jumps through";
		give SecretWindow general;
		" scared out of his wits.  And very glad to have them 
		back again.";
	],
	die [;
		StopDaemon(self);
		remove self;
		"You display this simple religious symbol and the 
		aggressive demon recoils in terror.  The horrifying 
		sight of this small, wooden cross causes the demon's 
		power to ebb.  In an instant the menacing apparition 
		fades into nothingness, leaving no tangible evidence of 
		his having been there.";
	],
	killplayer [ weapon;
		deadflag = 1;
		print "The menacing apparition bellows and laughs at your 
		pitiful ";
		if (weapon == nothing) {
			print "actions";
		} else {
			print (name) weapon;
		}
		".  He rapidly overpowers your spirit and makes you an 
		unwilling member of his undead realm.";
	],	
	life [;
	Give, Show:
		if (noun == Cross) {
			self.die();
			rtrue;
		}
			self.killplayer(noun);
			rtrue;
	Attack:
		if (second == Cross) {
			self.die();
		}
			self.killplayer(second);
			rtrue;
	],	
	before [;
	Smell:
		"It smells of choking sulphurous fumes mixed with 
		excrement and rotting flesh.";
	Taste:
		"Better not.";
	],
	has animate;
